plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.1.0'
}

group project_group
version project_version

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}


repositories {

    mavenCentral()

}
subprojects {
    ext {
        alibabaCloudVersion = '2.2.10-RC1'
        springCloudVersion = '2021.0.5'
    }

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'

    repositories {
        mavenCentral()
    }

    configurations {
        // 排除 tomcat 依赖（使用 jetty ）
        implementation.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    configurations.all {
        resolutionStrategy {
            force 'io.github.classgraph:classgraph:4.8.154'
        }
    }
    dependencies {
        if (project.name != "hxds-gateway") {
            //gateway 中已经依赖了web
            implementation 'org.springframework.boot:spring-boot-starter-web'
        }
        implementation 'org.springframework.boot:spring-boot-starter-jetty'
        compileOnly 'org.projectlombok:lombok'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

    }

    dependencyManagement {
        imports {
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${alibabaCloudVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    sourceCompatibility = project_jdk
    targetCompatibility = project_jdk
    tasks.named('test') {
        useJUnitPlatform()
    }
}